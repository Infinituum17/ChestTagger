plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=labellingcontainers"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // FastConfigAPI
    modApi("infinituum.fastconfigapi:fastconfigapi-fabric:${rootProject.minecraft_version}-${rootProject.fastconfigapi_version}")

    // Mythic Metals Decorations
    def ccapiVersion = "5.0.2"
    def aeaVersion = "1.4.0+1.19.4"
    def bejVersion = "1.2.2"

    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${ccapiVersion}"
    modCompileOnly "maven.modrinth:additionalentityattributes:${aeaVersion}"

    implementation "blue.endless:jankson:${bejVersion}"

    modCompileOnly "curse.maven:owo-lib-532610:4559399"
    modCompileOnly "curse.maven:mythicmetals-410127:4578506"
    modCompileOnly "curse.maven:alloy-forgery-438718:4487545"
    modCompileOnly "curse.maven:mythicmetals-decorations-552128:4570786"

    // Echo Chest mod
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-block:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${ccapiVersion}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-world:${ccapiVersion}"
    implementation 'com.electronwill.night-config:core:3.6.5'
    implementation 'com.electronwill.night-config:toml:3.6.5'
    modCompileOnly "curse.maven:forge-config-api-port-fabric-547434:4451343"
    modCompileOnly "curse.maven:puzzles-lib-495476:4582314"
    modCompileOnly "curse.maven:echo-chest-857972:4552567"

    // Nether Chested mod
    modCompileOnly "curse.maven:new-nether-chest-857971:4552679"

    // More Chests mod
    modCompileOnly "curse.maven:more-chests-737045:4977019"

    // Supplementaries mod
    modImplementation "curse.maven:selene-499980:4567754"
    modImplementation "curse.maven:supplementaries-412082:4558910"
}

processResources {
    var rep_properties = [
            "version"              : rootProject.mod_version,
            "mc_version"           : rootProject.minecraft_version,
            "arch_version"         : rootProject.architectury_version,
            "modid"                : rootProject.archives_base_name,
            "fastconfigapi_version": rootProject.fastconfigapi_version
    ]

    inputs.properties rep_properties

    filesMatching("fabric.mod.json") {
        expand rep_properties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
