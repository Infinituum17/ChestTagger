plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=labellingcontainers"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // Iron Chests: Restocked
    modImplementation "curse.maven:resourceful-lib-570073:4301209"
    modImplementation "curse.maven:ironchests-498794:4670971"

    // Mythic Metals Decorations
    def ccapiVersion = "5.0.2"
    def mixinextrasVersion = "0.2.0"
    def aeaVersion = "1.2.0+1.19.2"
    def reaVersion = "2.3.0"
    def bejVersion = "1.2.2"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${ccapiVersion}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${ccapiVersion}"
    modImplementation "io.github.llamalad7:mixinextras-fabric:${mixinextrasVersion}"
    modImplementation "maven.modrinth:additionalentityattributes:${aeaVersion}"
    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${reaVersion}"

    implementation "blue.endless:jankson:${bejVersion}"

    modImplementation "curse.maven:owo-lib-532610:4152949"
    modImplementation "curse.maven:cloth-config-348521:4633414"
    modImplementation "curse.maven:mythicmetals-410127:4441526"
    modImplementation "curse.maven:alloy-forgery-438718:4487529"
    modImplementation "curse.maven:mythicmetals-decorations-552128:4377086"
}

processResources {
    var rep_properties = [
            "version"     : project.version,
            "mc_version"  : project.minecraft_version,
            "arch_version": project.architectury_version,
            "modid"       : project.archives_base_name
    ]

    inputs.properties rep_properties

    filesMatching("fabric.mod.json") {
        expand rep_properties
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
